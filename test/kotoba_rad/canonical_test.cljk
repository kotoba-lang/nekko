(ns kotoba-rad.canonical-test
  (:require [clojure.test :refer [deftest is]]
  [nekko.bytes :as nb]
            [ed25519.core :as ed]
            [nekko.canonical :as canonical]
            [nekko.sigref :as sigref]))

(def seeds [(nb/->ba (repeat 32 1))
            (nb/->ba (repeat 32 2))
            (nb/->ba (repeat 32 3))])
(def delegates (set (map ed/did-key-from-seed seeds)))
(def policy {:rid "rid-ci" :ref "refs/ci/main" :delegates delegates :threshold 2})

(defn signed [seed commit ts]
  (sigref/sign seed "rid-ci" "refs/ci/main" commit ts))

(deftest threshold-requires-distinct-authorized-valid-signers
  (is (nil? (canonical/canonical-ref policy [(signed (first seeds) "c1" 1)])))
  (is (= "c1" (canonical/canonical-ref
                policy [(signed (first seeds) "c1" 1)
                        (signed (second seeds) "c1" 2)])))
  (is (nil? (canonical/canonical-ref
             policy [(signed (first seeds) "c1" 1)
                     (signed (first seeds) "c1" 2)])))
  (let [outsider (nb/->ba (repeat 32 9))]
    (is (nil? (canonical/canonical-ref
               policy [(signed (first seeds) "c1" 1)
                       (signed outsider "c1" 2)])))))

(deftest matching-rid-and-ref-are-required
  (let [valid (signed (first seeds) "c1" 1)
        wrong-ref (sigref/sign (second seeds) "rid-ci" "refs/heads/main" "c1" 2)]
    (is (nil? (canonical/canonical-ref policy [valid wrong-ref])))))

(deftest split-quorum-is-explicit-conflict
  (let [four-seeds (conj seeds (nb/->ba (repeat 32 4)))
        p (assoc policy :delegates (set (map ed/did-key-from-seed four-seeds)))]
    (is (thrown-with-msg?
         #?(:clj Exception :cljs js/Error) #"conflicting commits reached quorum"
         (canonical/canonical-ref
          p [(signed (nth four-seeds 0) "a" 1)
             (signed (nth four-seeds 1) "a" 2)
             (signed (nth four-seeds 2) "b" 3)
             (signed (nth four-seeds 3) "b" 4)])))))
