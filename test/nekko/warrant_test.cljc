(ns nekko.warrant-test
  (:require [clojure.test :refer [deftest is]]
            [nekko.bytes :as nb]
            [ed25519.core :as ed]
            [nekko.warrant :as warrant]))

(def signer-seed (nb/->ba (range 32)))
(def other-seed (nb/->ba (map #(mod (+ % 5) 256) (range 32))))
(def sample-event {"prev" "cid-0" "seq" 2 "old" "commit-1" "new" "commit-2" "signer" "did:key:zABC" "sig" "0a1b"})

(deftest issue-and-verify-roundtrip
  (let [w (warrant/issue signer-seed :seq-break "rid-1" "refs/heads/main" sample-event)]
    (is (true? (warrant/valid? w)))))

(deftest tampered-event-fails-verification
  (let [w (warrant/issue signer-seed :seq-break "rid-1" "refs/heads/main" sample-event)
        tampered (assoc w :warrant/event (assoc sample-event "seq" 99))]
    (is (false? (warrant/valid? tampered)))))

(deftest tampered-kind-fails-verification
  (let [w (warrant/issue signer-seed :seq-break "rid-1" "refs/heads/main" sample-event)]
    (is (false? (warrant/valid? (assoc w :warrant/kind :prev-mismatch))))))

(deftest swapped-signer-fails-verification
  (let [w (warrant/issue signer-seed :seq-break "rid-1" "refs/heads/main" sample-event)
        other-did (ed/did-key-from-seed other-seed)]
    (is (false? (warrant/valid? (assoc w :warrant/signer other-did))))))

(deftest missing-sig-is-invalid
  (is (false? (warrant/valid? {:warrant/kind :seq-break :warrant/rid "rid-1"}))))
