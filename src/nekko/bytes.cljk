(ns nekko.bytes
  "Portable byte helpers for kotoba-rad R2 (object encryption). :clj works in
   byte-arrays, :cljs (nbb / --target node) in js/Uint8Array — the same
   platform-native byte convention ed25519.core and cbor.core use. Hex is the
   cross-platform wire form for all keys / ciphertext."
  (:refer-clojure :exclude [cat])
  #?(:clj (:import (java.security MessageDigest SecureRandom))
     :cljs (:require ["crypto" :as ncrypto])))

(defn blen [b] #?(:clj (alength ^bytes b) :cljs (.-length b)))

(defn ->ba
  "Coerce to the platform byte type (byte-array / Uint8Array)."
  [xs]
  #?(:clj (if (bytes? xs) xs (byte-array xs))
     :cljs (if (instance? js/Uint8Array xs) xs (js/Uint8Array. (into-array xs)))))

(defn hexify [b]
  #?(:clj (apply str (map #(format "%02x" (bit-and (int %) 0xff)) b))
     :cljs (.toString (js/Buffer.from b) "hex")))

(defn unhex [^String s]
  #?(:clj (byte-array (map (fn [[a b]] (unchecked-byte (Integer/parseInt (str a b) 16)))
                           (partition 2 s)))
     :cljs (js/Uint8Array. (js/Buffer.from s "hex"))))

(defn utf8 [^String s]
  #?(:clj (.getBytes s "UTF-8")
     :cljs (.encode (js/TextEncoder.) s)))

(defn cat [a b]
  #?(:clj (let [a (->ba a) b (->ba b) out (byte-array (+ (alength a) (alength b)))]
            (System/arraycopy a 0 out 0 (alength a))
            (System/arraycopy b 0 out (alength a) (alength b))
            out)
     :cljs (js/Uint8Array. (js/Buffer.concat #js[(js/Buffer.from a) (js/Buffer.from b)]))))

(defn slice [b from to]
  #?(:clj (java.util.Arrays/copyOfRange ^bytes (->ba b) (int from) (int to))
     :cljs (.slice b from to)))

(defn tail
  "Last `n` bytes of b."
  [b n]
  (slice b (- (blen b) n) (blen b)))

(defn zeros [n]
  #?(:clj (byte-array n) :cljs (js/Uint8Array. n)))

(defn random-bytes [n]
  #?(:clj (let [b (byte-array n)] (.nextBytes (SecureRandom.) b) b)
     :cljs (js/Uint8Array. (ncrypto/randomBytes n))))

(defn sha256 [b]
  #?(:clj (.digest (MessageDigest/getInstance "SHA-256") (->ba b))
     :cljs (js/Uint8Array. (-> (ncrypto/createHash "sha256")
                               (.update (js/Buffer.from b))
                               (.digest)))))

(defn equal? [a b]
  #?(:clj (java.util.Arrays/equals ^bytes (->ba a) ^bytes (->ba b))
     :cljs (= (hexify a) (hexify b))))
